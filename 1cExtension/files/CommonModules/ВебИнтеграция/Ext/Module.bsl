&Вместо("ПолучитьВыгружаемыеВидыВремени")
Функция ПолучитьВыгружаемыеВидыВремениCustom(ОтключитьДетализациюСвободногоВремениДоВидаВремени)
	
	ДляСобственнойИнтеграции = integrationFunctions.getCustomIntegrationFlag();
	
	ВыгружаемыеВидыВремени = Новый Массив;
	
	Если ДляСобственнойИнтеграции Тогда
		ВыгружаемыеВидыВремени = integrationFunctions.getAllowedTypesOfTime();
		Если ВыгружаемыеВидыВремени.Количество() = 0 Тогда
			ВыгружаемыеВидыВремени = УправлениеЗаявками.ПолучитьРабочиеВидыВремени();
		КонецЕсли;
	Иначе
		ВыгружаемыеВидыВремени = ПродолжитьВызов();
	КонецЕсли;	
	
	Возврат ВыгружаемыеВидыВремени;
	
КонецФункции  

&Вместо("ПолучитьДоступныхСотрудников")
Функция ПолучитьДоступныхСотрудниковCustom()   
	ДляСобственнойИнтеграции = integrationFunctions.getCustomIntegrationFlag();
	
	Если ДляСобственнойИнтеграции Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.УчаствуетВКалендарномПланировании
		|	И НЕ Сотрудники.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудники.Наименование";
		
		РезультатЗапроса = Запрос.Выполнить();
		Выгрузка = РезультатЗапроса.Выгрузить();
		ДоступныеСотрудники = Выгрузка.ВыгрузитьКолонку("Ссылка");
		Возврат ДоступныеСотрудники;
	Иначе
		Возврат ПродолжитьВызов();	
	КонецЕсли;
КонецФункции

&После("ПолучитьГрафикиРаботыXML")
Процедура ОтключитьФлагСобственнойИнтеграции(ДатаНачала, ДатаОкончания, ОписаниеОшибки, ИнтеграцияССайтом = Ложь)
	integrationFunctions.setCustomIntegrationFlag(false);
КонецПроцедуры

&Вместо("ПолучитьТаблицуСотрудников")
Функция ПолучитьТаблицуСотрудниковCustom(НастройкиОбменаССайтом)   
	ДляСобственнойИнтеграции = integrationFunctions.getCustomIntegrationFlag();
	
	Если ДляСобственнойИнтеграции Тогда
		НастройкиОбменаССайтом = Неопределено;
		integrationFunctions.setCustomIntegrationFlag(false);
	КонецЕсли;
	Возврат ПродолжитьВызов(НастройкиОбменаССайтом);
КонецФункции
